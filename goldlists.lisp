
(defpackage #:goldlists
  (:use #:cl
        #:iterate)
  (:export :circular-list
           :immutable-circular-list
           :print-object
           :make-circular-list
           :clist-equal
           :clist-focused
           :clist-insert
           :clist-rotate
           :clist-remove-focused))

(in-package #:goldlists)

(defclass circular-list ()
  ((current :initarg :current))
  (:documentation
   "A circular list represents a series of objects that conceptually form
   a ring.  The *focus* of a circular list allows O(1) access to the
   focused object and O(1) insertion adjacent to it.  A circular list may
   be *rotated* to shift the focus."))

(defclass immutable-circular-list (circular-list)
  ((left :initarg :left
         :initform nil)
   (right :initarg :right
          :initform nil))
  (:documentation
   "This circular list implementation is immutable.  All of the
   modification methods return a new list (which might share structure
   with the old one)."))

(defclass mutable-circular-list (circular-list)
  ((left :initarg :left
         :initform nil)
   (right :initarg :right
          :initform nil))
  (:documentation
   "This circular list implementation is mutable."))

(defmethod print-object ((clist immutable-circular-list) stream)
  (labels ((summarize-list (n list)
             (cond
               ((endp list)
                nil)
               ((zerop n)
                '("..."))
               (t
                (cons (car list) (summarize-list (1- n) (cdr list)))))))
    (with-slots (left current right) clist
      (let ((left-summary (reverse (summarize-list 2 left)))
            (right-summary (summarize-list 2 right)))
        (format stream "#<IMMUTABLE-CIRCULAR-LIST [A]［夯蔺窿劲戾骠篚眄狎沲蝌孱蜷玷舡篚眄狎┅┅ㄤ彐礤翳镤痱轭舡镡赍泗è沆轶眭翎忪瀛汩蜚蹯狎扉篝篝蝈犴ㄦ矧磲篝蝈犴＜驼粤绿怒蒙颐仗烈躺釉埝凛劲ㄣ扉篝骘沲箦箪雉鲠祯箪雉鲠祯沆轶ъ彐舂ъ彐舂ㄣ扉篝骘沲箦箪雉鲠祯沆轶ъ彐舂ㄣ扉篝骘沲箦沆轶舂ㄣ扉篝骘沲箦箪雉鲠祯沆轶蜷玷舂ㄣ扉篝骘沲箦箪雉鲠祯箪雉鲠祯沆轶蜷玷舂蜷玷舂┅ㄤ彐躅磲脲汩蜚蹯狎扉篝ㄩ铋糸犰屐屙孱舡矧箦聃孱沐脲眭翎忪铋飑⒚蝈狒弩汩蜚蹯狎扉篝涉晌稍闪汰盘磐盼原弦优颜盼门轶箦聃孱沐翳骘沲鏖祆忮镱翳骈蝮屐屙孱轭翳箦聃孱沐五篝邃箦聃孱沐狎铒篚痧矧翦洚麒孱ㄡ钿眭翎忪豉疱轭轸獒飙屐屙孱舡矧箦聃孱沐箦聃孱沐豉疱ㄥ祠轭轸獒飙屐屙孱舡矧箦聃孱沐癌箦聃孱沐┅ㄥ蝌矧⑽弩翦箦聃孱沐狎铒篚痧矧翦骘眭翎忪汩蜚蹯狎扉篝螽┅ㄩ眭翎忪ㄩ豉疱轭轸獒飙屐屙孱舡矧箦聃孱沐箦聃孱沐戾è沆轶磲脲汩蜚蹯狎扉篝ㄥ祠轭轸獒飙屐屙孱舡矧箦聃孱沐癌喉豸徕戾舂┅ㄩ翦ㄦ矧轭箦聃孱沐篚怏羼轭轸獒飙屐屙孱舡矧箦聃孱沐暴ㄦ矧沲蝌孱舡沆轶骈蝮ㄣ扉篝轭箦螋沆轶候殓梏翳孱ㄣ扉篝轭箦螋沲蝌孱舡沆轶候殓梏┅沆轶舂戾è沆轶磲脲轭篝犷沐ы豸徕戾汩蜚蹯狎扉篝恒躜蝈铘轭轸獒飙屐屙孱舡矧箦聃孱沐┅箦翩箪雉鲠祯沆轶ъ彐舂沆轶舂箦翩箪雉鲠祯沆轶蜷玷舂沆轶舂沆轶舂ㄩ豉疱轭轸獒飙屐屙孱舡矧箦聃孱沐箦聃孱沐戾è轭轸獒飙扉篝磲ъ轶＇殇孱糸豉轭轸獒飙屐屙孱舡矧箦聃孱沐┅磲脲轭篝犷沐ч眄豸徕戾汩蜚蹯狎扉篝恒躜蝈铘ㄣ狎轭轸獒飙扉篝候殓梏ㄣ潋轭轸獒飙扉篝┅磲脲轭篝犷沐ч眄豸徕戾汩蜚蹯狎扉篝恒躜蝈铘轭轸獒飙屐屙孱舡矧箦聃孱沐┅┅ㄤ彐珏铄蜷沆轶舡羼踽ㄣ扉篝沆轶舨ê滹沲礤铘狒轱⒁弭躜铙殒怙翳汩蜚蹯狎扉篝栳鲥翳筢礤泔铘孱趔犷翳筢礤骘沲螽ê礤翳镤镡瓯镡瓴铋飑ㄤ彐礤翳镤沆轶舡羼踽è沆轶舯轫眭翎忪瀛汩蜚蹯狎扉篝ㄣ扉篝轫眭翎忪瀛汩蜚蹯狎扉篝┅鏖翳箪雉è戾骠戾骠ㄣ躜蝈铘沲蝌孱舂蜷玷舯蜷玷舂沆轶舯鏖翳箪雉è戾骠戾骠ㄣ躜蝈铘沲蝌孱舂蜷玷舨蜷玷舂沆轶舨ㄡ钿ㄥ聃犰沲蝌孱舯沲蝌孱舨ㄥ聃犰ㄡ痧孱蜷玷舯蝈鲥蝮戾骠暴ㄡ痧孱蜷玷舨蝈鲥蝮戾骠博┅┅┅ㄤ彐礤翳镤沆轶舡羼踽è沆轶舯眭翎忪瀛汩蜚蹯狎扉篝ㄣ扉篝眭翎忪瀛汩蜚蹯狎扉篝┅ㄡ钿ㄥ聃犰ㄣ扉篝骘沲箦沆轶舯ㄣ扉篝骘沲箦沆轶舨┅ㄩ翦ㄦ矧惚骈蝮ㄣ扉篝蝻翎翦沆轶舯暴翳孱ㄣ扉篝蝻翎翦惚暴ㄦ矧悴骈蝮ㄣ扉篝蝻翎翦沆轶舨暴翳孱ㄣ扉篝蝻翎翦悴暴躅糸ㄥ惚沆轶舯┅ㄡ祺狴ㄥ聃犰ㄣ扉篝骘沲箦惚ㄣ扉篝骘沲箦悴┅┅┅ㄤ彐珏铄蜷沆轶舡骘沲箦ㄣ扉篝ê滹沲礤铘狒轱⒁弭躜铙翳骘沲箦镡赍泗轭翳汩蜚蹯狎扉篝┅ㄤ彐礤翳镤沆轶舡骘沲箦è沆轶汩蜚蹯狎扉篝┅箪雉鲠祯沆轶с躜蝈铘┅ㄤ彐珏铄蜷沆轶舡轭箦螋ㄣ扉篝铄鳝镡赍泗镳糸镱犰痫箝糸镱ê滹沲礤铘狒轱⒘滗闻篆下逝迷麸汩蜚蹯狎扉篝犷蝈趱蝾翳铄扉篝澡铄黛徜溴镡赍泗鏖祆忮骘沲箦洚涉邢由陨衔轶红彐衄翳镡赍泗鏖祆忮徜溴麸翳戾骠镦翳痱弼轱躞禊骘沲箦镡赍泗涉邢由陨衔轶候殓梏翳镡赍泗鏖祆忮徜溴麸翳蜷玷舢┅ㄤ彐礤翳镤沆轶舡轭箦螋è沆轶轫眭翎忪瀛汩蜚蹯狎扉篝铄鳝镡赍泗镳糸镱犰痫箝糸镱红彐舂麒孱ㄡ钿铒ㄥ痫箝糸镱红彐舂铒ㄥ痫箝糸镱候殓梏┅ㄥ蝌矧⑿嫌稍上眭篝忮彘翳弪红彐矧候殓梏铒立痫箝糸镱┅鏖翳箪雉戾骠沲蝌孱蜷玷舂沆轶ㄩㄥ痫箝糸镱红彐舂磲脲轭篝犷沐ч眄豸徕戾汩蜚蹯狎扉篝红彐戾骠恒躜蝈铘铄鳝镡赍泗候殓梏ㄣ镱沲蝌孱蜷玷舂磲脲轭篝犷沐ч眄豸徕戾汩蜚蹯狎扉篝红彐ㄣ镱沲蝌孱戾骠恒躜蝈铘铄鳝镡赍泗候殓梏蜷玷舂┅ㄤ彐礤翳镤沆轶舡轭箦螋è沆轶眭翎忪瀛汩蜚蹯狎扉篝铄鳝镡赍泗镳糸镱犰痫箝糸镱红彐舂麒孱ㄡ钿铒ㄥ痫箝糸镱红彐舂铒ㄥ痫箝糸镱候殓梏┅ㄥ蝌矧⑿嫌稍上眭篝忮彘翳弪红彐矧候殓梏铒立痫箝糸镱┅鏖翳箪雉戾骠沲蝌孱蜷玷舂沆轶戾è铄鳝铒溴ㄩㄥ痫箝糸镱红彐舂磲脲轭篝犷沐ы豸徕戾汩蜚蹯狎扉篝红彐戾骠恒躜蝈铘铄鳝镡赍泗候殓梏沆轶舂磲脲轭篝犷沐ы豸徕戾汩蜚蹯狎扉篝红彐沆轶恒躜蝈铘铄鳝镡赍泗候殓梏蜷玷舂┅箦翩箪雉鲠祯箪雉鲠祯铄鳝铒溴蜷玷舂ъ彐舂铄鳝铒溴箦翩箪雉鲠祯箪雉鲠祯铄鳝铒溴ъ彐舂蜷玷舂铄鳝铒溴铄鳝铒溴┅ㄤ彐珏铄蜷沆轶舡蝻翎翦ㄣ扉篝瞟ê滹沲礤铘狒轱⑼秭弩翳骘沲篝屦麸翳蜷玷犷蝈趱蝾翳铄黛骘沲箦汩蜚蹯狎扉篝磲忮铄玑糸鲥麒殂鏖祆盹鲥翳骘沲麸翳戾骠┊┅ㄤ彐礤翳镤沆轶舡蝻翎翦è沆轶轫眭翎忪瀛汩蜚蹯狎扉篝瞟鏖翳箪雉戾骠沲蝌孱蜷玷舂沆轶ㄣ镱è弪镳瞟沆轶舂è痨躞瞟ㄤ弩趄蹉趱蜷铉忾钿铄鳝戾骠铄鳝蜷玷舂ㄩㄥ钿蜷玷舂扉篝铋蝈鲥蝮ㄣ镱沲蝌孱戾骠┅扉篝ㄣ镱沲蝌孱戾骠蜷玷舂ㄣ扉篝蝻翎翦磲脲轭篝犷沐ч眄豸徕戾汩蜚蹯狎扉篝红彐铄鳝戾骠恒躜蝈铘ㄣ狎铄鳝蜷玷舂候殓梏ㄣ潋铄鳝蜷玷舂ū瞟┅è黹铛箴瞟ㄤ弩趄蹉趱蜷铉忾钿铄鳝戾骠铄鳝蜷玷舂ㄩㄥ钿戾骠扉篝蝈鲥蝮ㄣ镱沲蝌孱蜷玷舂铋飑扉篝戾骠ㄣ镱沲蝌孱蜷玷舂┅ㄣ扉篝蝻翎翦磲脲轭篝犷沐ч眄豸徕戾汩蜚蹯狎扉篝红彐ㄣ潋铄鳝戾骠恒躜蝈铘ㄣ狎铄鳝戾骠候殓梏铄鳝蜷玷舂ū瞟┅┅┅ㄤ彐礤翳镤沆轶舡蝻翎翦è沆轶眭翎忪瀛汩蜚蹯狎扉篝瞟鏖翳箪雉戾骠沲蝌孱蜷玷舂沆轶ㄣ镱è弪镳瞟沆轶舂è痨躞瞟ㄣ扉篝蝻翎翦蜷玷ū瞟┅è黹铛箴瞟ㄣ扉篝蝻翎翦戾骠ū瞟┅┅ㄤ彐珏铄蜷沆轶舡蝈盹鲥骘沲箦ㄣ扉篝ê滹沲礤铘狒轱⒁屙秭弩翳骘沲箦鲠祯犷蝈痨徙弩轸鏖翳翳鲠祯麸轸蜷玷舢义趱蝾赭鲠祯弩汩蜚蹯狎扉篝鏖翳翳鲠祯蝈盹鲥浠犷翳鲠祯翳狒麽蝈盹鲥洚义盹鲩铉翳灬篝鲠祯轭汩蜚蹯狎扉篝戾徜麸躅溴骈铄忮栳鲩矧┅ㄤ彐礤翳镤沆轶舡蝈盹鲥骘沲箦è沆轶轫眭翎忪瀛汩蜚蹯狎扉篝┅鏖翳箪雉戾骠沲蝌孱蜷玷舂沆轶鲠祯弩ㄤ弩趄蹉趱蜷铉忾钿铄鳝戾骠铄鳝蜷玷舂ㄩㄥ钿蜷玷舂扉篝铋蝈鲥蝮戾骠┅扉篝戾骠蜷玷舂磲脲轭篝犷沐ч眄豸徕戾汩蜚蹯狎扉篝红彐铄鳝戾骠恒躜蝈铘ㄣ狎铄鳝蜷玷舂候殓梏ㄣ潋铄鳝蜷玷舂┅沲蝌孱舂┅ㄤ彐礤翳镤沆轶舡蝈盹鲥骘沲箦è沆轶眭翎忪瀛汩蜚蹯狎扉篝┅鏖翳箪雉戾骠沲蝌孱蜷玷舂沆轶箦翩箪雉鲠祯蜷玷ъ彐舂戾骠箦翩箪雉鲠祯戾骠蜷玷舂蜷玷舂鲠祯弩蜷玷沲蝌孱舂┅